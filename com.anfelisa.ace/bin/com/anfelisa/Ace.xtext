grammar com.anfelisa.Ace with org.eclipse.xtext.common.Terminals

generate ace "http://www.anfelisa.com/Ace"

Project: 
	name = ID
	('actions' '{'
		(actions += Action)*
	'}')*
	('commands' '{'
		(commands += Command)*
	'}')*
	('events' '{'
		(events += Event)*
	'}')*
	('views' '{'
		(views += View)*
	'}')*
;

Action:
	name = ID 
	('executes' command = [Command])? 
;

Command:
	name = ID ('{'
		(eventsOnOutcome += EventOnOutcome)*
	'}')*
;

EventOnOutcome:
	'on' outcome = ID 
	('publishes' '(' (events += [Event | ID])* ')')? 
	('triggers' '(' (actions += [Action | ID])* ')')? 
;

Event:
	name = ID 
	('listenedToBy' '(' (listeners += [RenderFunction | QualifiedName])* ')')? 
;

View: 
	name = ID ('{'
		(renderFunctions += RenderFunction)*
	'}')*
;

RenderFunction:
	name = ID
;

QualifiedName:
  ID ('.' ID)*
;

