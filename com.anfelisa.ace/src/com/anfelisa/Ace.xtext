grammar com.anfelisa.Ace with org.eclipse.xtext.common.Terminals

generate ace "http://www.anfelisa.com/Ace"

Project: 
	{Project} 'ES6' es6 = ES6 ? | 'JAVA' java = JAVA ?
;

ES6: 
	name = QualifiedName
	('ACE' (aceOperations += ES6_ACE)*)?
	('import' appStateRef = [ES6_StateElement | QualifiedName])?
	('appState' appState = ES6_StateElement )?
;

ES6_ACE: 
	name = ID  ('<' input += ID (',' input += ID)* '>')?  
	('call' serverCall = [JAVA_ACE | QualifiedName] )? 
	('loadingFlag' loadingFlag = [ES6_StateElement | QualifiedName])? 
	(outcomes += ES6_Outcome)*
;

ES6_Outcome:
	'on' name = ID 
	('(' listeners += ES6_StateFunction* ')')? 
	('triggers' '(' (aceOperations += [ES6_ACE | QualifiedName])* ')')? 
;

ES6_StateFunction:
	stateFunctionType = ES6_StateFunctionType attribute = [ES6_StateElement | QualifiedName]
;

ES6_StateFunctionType:
	'set' | 'merge' | 'reset'
;

ES6_StateElement:
	name = ID list ?= 'list'? hash ?= 'location.hash'? storage ?= 'storage'? (types += ES6_TypeDefinition ('|' types += ES6_TypeDefinition)*)?
;

ES6_TypeDefinition:
	':' name = ID '{'
		elements += ES6_StateElement* 
	'}'
;

JAVA:
	name = QualifiedName
	('Authorization' authUser = AuthUser)?
	('import' authUserRef = [AuthUser | QualifiedName])?
	('ACE' (aceOperations += JAVA_ACE)*	)?
	('views' (views += JAVA_View)* )?
	('models' (models += Model)*)?
;

JAVA_ACE: 
	JAVA_ACE_WRITE | JAVA_ACE_READ
;

JAVA_ACE_WRITE: 
	proxy ?= 'proxy'? name = ID '<' model = [ Model | QualifiedName ] '>' 
	type = WriteFunctionType
	url = STRING
	authorize ?= 'authorize'? 
	('pathParams' (pathParams += [Attribute | QualifiedName])*)?
	('queryParams' (queryParams += [Attribute | QualifiedName])*)?
	('payload' (payload += [Attribute | QualifiedName])*)?
	('response' (response += [Attribute | QualifiedName])*)?
	(outcomes += JAVA_Outcome)*
;

JAVA_Outcome:
	'on' name = ID 
	('('(listeners += [JAVA_ViewFunction | QualifiedName])*')')* 
	('triggers' '(' (aceOperations += [JAVA_ACE_WRITE | QualifiedName])* ')')? 
;

JAVA_ACE_READ: 
	proxy ?= 'proxy'? name = ID '<' model = [ Model | QualifiedName ] '>' 
	type = ReadFunctionType
	url = STRING
	authorize ?= 'authorize'? 
	('pathParams' (pathParams += [Attribute | QualifiedName])*)?
	('queryParams' (queryParams += [Attribute | QualifiedName])*)?
	('payload' (payload += [Attribute | QualifiedName])*)?
	('response' (response += [Attribute | QualifiedName])*)?
;

JAVA_View: 
	name = ID 
	('{'
		(renderFunctions += JAVA_ViewFunction)*
	'}')?		
;

JAVA_ViewFunction:
	name = ID '(' model = [ Model | QualifiedName ] ')'
;

AuthUser: 
	name = ID
	 ('{'
		attributes += Attribute*
	'}')
;

Model:
	persistent ?= 'persistent'? name = ID ('extends' superModels+=[Model|QualifiedName] (',' superModels+=[Model|QualifiedName])* )? ('{'
		attributes += Attribute*
	'}')?
;

Attribute:
	unique ?= 'Unique'? primaryKey ?= 'PrimaryKey'? (constraint = Constraint)? list ?= 'List'? (type = Type | model = [Model | QualifiedName]) name = ID ('references' foreignKey = [Attribute | QualifiedName])?
;

QualifiedName:
  ID ('.' ID)*
;

WriteFunctionType:
	'POST' | 'PUT' | 'DELETE'
;

ReadFunctionType:
	'GET'
;

Type:
	'Integer' | 'String' | 'Float' | 'Boolean' | 'DateTime' | 'Long'
;

Constraint:
	'NotEmpty' | 'NotNull'
;

