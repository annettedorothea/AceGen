grammar com.anfelisa.Ace with org.eclipse.xtext.common.Terminals

generate ace "http://www.anfelisa.com/Ace"

Project: 
	name = QualifiedName
	'target' target = ('ES6' | 'JAVA')
	('models' '{'
		(models += Model)*
	'}')*
	('data' '{'
		(data += Data)*
	'}')*
	('actions' '{'
		(actions += Action)*
	'}')*
	('commands' '{'
		(commands += Command)*
	'}')*
	('events' '{'
		(events += Event)*
	'}')*
	('views' '{'
		(views += View)*
	'}')*
;

Model:
	persistent ?= 'persistent'? name = ID '('
		(attributes += Attribute)*
	')'
;

Data:
	name = ID ':' '('
		(models += ModelRef)*
	')'
	(':' '('
		(dataLists += DataRef)*
	')')?
;

ModelRef: 
	list ?= 'List'? model = [Model | QualifiedName]
;

DataRef: 
	'List' data = [Data | QualifiedName]
;

Attribute:
	unique ?= 'Unique'? primaryKey ?= 'PrimaryKey'? (constraint = Constraint)? (type = ModelType) name = ID ('references' foreignKey = [Attribute | QualifiedName])?
;

Action:
	(type = FunctionType)? name = ID 
	('uses' data = [ Data | QualifiedName ])?
	('executes' command = [Command | QualifiedName])? 
;

Command:
	name = ID 
	('uses' data = [ Data | QualifiedName ])? 
	('{'
		(eventsOnOutcome += EventOnOutcome)*
	'}')*
;

EventOnOutcome:
	'on' outcome = ID 
	('publishes' '(' (events += [Event | QualifiedName])* ')')? 
	('triggers' '(' (actions += [Action | QualifiedName])* ')')? 
;

Event:
	name = ID 
	('uses' data = [ Data | QualifiedName ])? 
	('listenedToBy' '(' (listeners += [ViewFunction | QualifiedName])* ')')? 
;

View: 
	name = ID ('{'
		(renderFunctions += ViewFunction)*
	'}')*
;

ViewFunction:
	name = ID ('(' data = [ Data | QualifiedName ] ')')?
;

QualifiedName:
  ID ('.' ID)*
;

FunctionType:
	'POST' | 'PUT' | 'DELETE' | 'GET'
;

ModelType:
	'Serial' | 'Integer' | 'String' | 'Float' | 'Boolean' | 'DateTime' | 'Long' 
;

Constraint:
	'NotEmpty' | 'NotNull'
;

