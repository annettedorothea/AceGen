grammar com.anfelisa.Ace with org.eclipse.xtext.common.Terminals

generate ace "http://www.anfelisa.com/Ace"

Project: 
	name = QualifiedName
	'target' target = ('ES6' | 'JAVA')
	('ACE' '{'
		(aceOperations += ACE)*
	'}')?
	('views' '{'
		(views += View)*
	'}')?
	('models' '{'
		'schema' schema = ID
		(models += Model)*
	'}')?
	('data' '{'
		(data += Data)*
	'}')?
;

ACE: 
	init ?= 'init'?
	(type = FunctionType)? name = ID 
	('uses' data = [ Data | QualifiedName ])?
	('preUpdateUI' '(' (PreUpdateUI += [ViewFunction | QualifiedName])* ')')? 
	('postUpdateUI' '(' (PostUpdateUI += [ViewFunction | QualifiedName])* ')')? 
	('{'
		(outcomes += Outcome)*
	'}')*
;

Outcome:
	'on' name = ID 
	('(' (listeners += [ViewFunction | QualifiedName])* ')')? 
	('triggers' '(' (aceOperations += [ACE | QualifiedName])* ')')? 
;

Model:
	persistent ?= 'persistent'? name = ID ('{'
		attributes += Attribute*
	'}')?
;

Data:
	name = ID ':' '('
		(models += ModelRef)*
	')'
;

ComplexAttribute: 
	list ?= 'List'? model = [Model | QualifiedName] name = ID
;

ModelRef: 
	model = [Model | QualifiedName]
;

PrimitiveAttribute :
	unique ?= 'Unique'? primaryKey ?= 'PrimaryKey'? (constraint = Constraint)? list ?= 'List'? (type = ModelType) name = ID ('references' foreignKey = [Attribute | QualifiedName])?
;

Attribute:
	complexAttribute = ComplexAttribute | primitiveAttribute = PrimitiveAttribute
;

View: 
	external ?= 'external'? name = ID ('{'
		(renderFunctions += ViewFunction)*
	'}')*
;

ViewFunction:
	name = ID ('(' data = [ Data | QualifiedName ] ')')?
;

QualifiedName:
  ID ('.' ID)*
;

FunctionType:
	'POST' | 'PUT' | 'DELETE' | 'GET'
;

ModelType:
	'Serial' | 'Integer' | 'String' | 'Encrypted' | 'Float' | 'Boolean' | 'DateTime' | 'Long' 
;

Constraint:
	'NotEmpty' | 'NotNull'
;

