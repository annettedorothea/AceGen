grammar com.anfelisa.Ace with org.eclipse.xtext.common.Terminals

generate ace "http://www.anfelisa.com/Ace"

Project: 
	name = QualifiedName
	'target' target = ('ES6' | 'JAVA')
	(aceOperations += ACE)*
	('actions' '{'
		(actions += Action)*
	'}')?
	('commands' '{'
		(commands += Command)*
	'}')?
	('events' '{'
		(events += Event)*
	'}')?
	('views' '{'
		(views += View)*
	'}')?
	('models' '{'
		'schema' schema = ID
		(models += Model)*
	'}')?
	('data' '{'
		(data += Data)*
	'}')?
;

ACE: 
	init ?= 'init'?
	(type = FunctionType)? name = ID 
	('uses' data = [ Data | QualifiedName ])?
	('preUpdateUI' '(' (PreUpdateUI += [ViewFunction | QualifiedName])* ')')? 
	('postUpdateUI' '(' (PostUpdateUI += [ViewFunction | QualifiedName])* ')')? 
	('{'
		(outcomes += Outcome)*
	'}')*
;

Outcome:
	'on' name = ID 
	('(' (listeners += [ViewFunction | QualifiedName])* ')')? 
	('triggers' '(' (aceOperations += [ACE | QualifiedName])* ')')? 
;

Model:
	persistent ?= 'persistent'? name = ID ('{'
		('primitiveAttributes' '{' 
			attributes += Attribute*
			
		'}')?
		('complexAttributes' '{' 
			models += ComplexAttribute*
		'}')?
	'}')?
;

Data:
	name = ID ':' '('
		(models += ModelRef)*
	')'
;

ComplexAttribute: 
	list ?= 'List'? model = [Model | QualifiedName] name = ID
;

ModelRef: 
	model = [Model | QualifiedName]
;

Attribute:
	unique ?= 'Unique'? primaryKey ?= 'PrimaryKey'? (constraint = Constraint)? list ?= 'List'? (type = ModelType) name = ID ('references' foreignKey = [Attribute | QualifiedName])?
;

Action:
	init ?= 'init'?
	(type = FunctionType)? name = ID 
	('uses' data = [ Data | QualifiedName ])?
	('preUpdateUI' '(' (PreUpdateUI += [ViewFunction | QualifiedName])* ')')? 
	('executes' command = [Command | QualifiedName])? 
	('postUpdateUI' '(' (PostUpdateUI += [ViewFunction | QualifiedName])* ')')? 
;

Command:
	name = ID 
	('uses' data = [ Data | QualifiedName ])? 
	('{'
		(eventsOnOutcome += EventOnOutcome)*
	'}')*
;

EventOnOutcome:
	'on' outcome = ID 
	('publishes' '(' (events += [Event | QualifiedName])* ')')? 
	('triggers' '(' (actions += [Action | QualifiedName])* ')')? 
;

Event:
	name = ID 
	('uses' data = [ Data | QualifiedName ])? 
	('listenedToBy' '(' (listeners += [ViewFunction | QualifiedName])* ')')? 
;

View: 
	external ?= 'external'? name = ID ('{'
		(renderFunctions += ViewFunction)*
	'}')*
;

ViewFunction:
	name = ID ('(' data = [ Data | QualifiedName ] ')')?
;

QualifiedName:
  ID ('.' ID)*
;

FunctionType:
	'POST' | 'PUT' | 'DELETE' | 'GET'
;

ModelType:
	'Serial' | 'Integer' | 'String' | 'Encrypted' | 'Float' | 'Boolean' | 'DateTime' | 'Long' 
;

Constraint:
	'NotEmpty' | 'NotNull'
;

