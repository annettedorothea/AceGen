grammar com.anfelisa.Ace with org.eclipse.xtext.common.Terminals

generate ace "http://www.anfelisa.com/Ace"

Project: 
	name = QualifiedName
	'target' target = ('PHP' | 'ES6' | 'JAVA')
	('actions' '{'
		(actions += Action)*
	'}')*
	('commands' '{'
		(commands += Command)*
	'}')*
	('events' '{'
		(events += Event)*
	'}')*
	('views' '{'
		(views += View)*
	'}')*
;

Action:
	(type = FunctionType)? name = ID 
	('executes' command = [Command | QualifiedName])? 
;

Command:
	name = ID ('{'
		(eventsOnOutcome += EventOnOutcome)*
	'}')*
;

EventOnOutcome:
	'on' outcome = ID 
	('publishes' '(' (events += [Event | QualifiedName])* ')')? 
	('triggers' '(' (actions += [Action | QualifiedName])* ')')? 
;

Event:
	name = ID 
	('listenedToBy' '(' (listeners += [ViewFunction | QualifiedName])* ')')? 
;

View: 
	name = ID ('{'
		(renderFunctions += ViewFunction)*
	'}')*
;

ViewFunction:
	name = ID
;

QualifiedName:
  ID ('.' ID)*
;

FunctionType:
	'CREATE' | 'READ' | 'UPDATE' | 'DELETE'
;

